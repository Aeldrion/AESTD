# Written by Aeldrion, Minecraft 1.14.4
# Displays a timer in the executing player's actionbar, based on their aestd.math.in score (in ticks)

function aestd:math/ticks_to_time

execute if score #aestd.hours aestd.time matches 0 if score #aestd.minutes aestd.time matches ..9 if score #aestd.seconds aestd.time matches ..9 run title @s actionbar ["0",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 0 if score #aestd.minutes aestd.time matches ..9 if score #aestd.seconds aestd.time matches 10.. run title @s actionbar ["0",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 0 if score #aestd.minutes aestd.time matches 10.. if score #aestd.seconds aestd.time matches ..9 run title @s actionbar ["",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 0 if score #aestd.minutes aestd.time matches 10.. if score #aestd.seconds aestd.time matches 10.. run title @s actionbar ["",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]

execute if score #aestd.hours aestd.time matches 1.. if score #aestd.minutes aestd.time matches ..9 if score #aestd.seconds aestd.time matches ..9 run title @s actionbar [{"score":{"name":"#aestd.hours","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 1.. if score #aestd.minutes aestd.time matches ..9 if score #aestd.seconds aestd.time matches 10.. run title @s actionbar [{"score":{"name":"#aestd.hours","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 1.. if score #aestd.minutes aestd.time matches 10.. if score #aestd.seconds aestd.time matches ..9 run title @s actionbar [{"score":{"name":"#aestd.hours","objective":"aestd.time"}},":",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":0",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
execute if score #aestd.hours aestd.time matches 1.. if score #aestd.minutes aestd.time matches 10.. if score #aestd.seconds aestd.time matches 10.. run title @s actionbar [{"score":{"name":"#aestd.hours","objective":"aestd.time"}},":",{"score":{"name":"#aestd.minutes","objective":"aestd.time"}},":",{"score":{"name":"#aestd.seconds","objective":"aestd.time"}}]
